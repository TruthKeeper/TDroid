import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.application'
apply from: rootProject.file('module.gradle')
//读取打包签名
def keystorePropertiesFile = rootProject.file("local.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    //打包签名
    signingConfigs {
        debug {
            storeFile file(keystoreProperties['keystore.path'])
            storePassword keystoreProperties['keystore.password']
            keyAlias keystoreProperties['keystore.alias']
            keyPassword keystoreProperties['keystore.alias_password']
        }
        release {
            storeFile file(keystoreProperties['keystore.path'])
            storePassword keystoreProperties['keystore.password']
            keyAlias keystoreProperties['keystore.alias']
            keyPassword keystoreProperties['keystore.alias_password']
        }
    }
    //默认打包名称
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, rootProject.getName()
                    + "-" + buildType.name
                    + "-" + releaseTime()
                    + "-v" + defaultConfig.versionName
                    + "-" + defaultConfig.versionCode
                    + ".apk");
        }
    }
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "com.tk.tdroiddemo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
//            manifestPlaceholders = [
//                    "holder_1": "aaa",
//                    "holder_2": "bbb",
//            ]
            signingConfig signingConfigs.debug
        }
        release {
//            manifestPlaceholders = [
//                    "holder_1": "111",
//                    "holder_2": "222",
//            ]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
//        a {
//            resValue "string", "res", "111111"
//            buildConfigField "String", "test", "\"aaa\""
//        }
//        b {
//            resValue "string", "res", "222222"
//            buildConfigField "String", "test", "\"bbb\""
//        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    //依赖的核心
    compile project(':library')
    //依赖的APT处理
    annotationProcessor project(':APT')
    //AOP的支持
    compile rootProject.ext.dependencies["aspect"]
}
//Aspect处理
final def log = project.logger
final def variants = project.android.applicationVariants
variants.all { variant ->
    if (!variant.buildType.isDebuggable()) {
        log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
        return;
    }

    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.7",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
        log.debug "ajc args: " + Arrays.toString(args)

        MessageHandler handler = new MessageHandler(true);
        new Main().run(args, handler);
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break;
                case IMessage.WARNING:
                    log.warn message.message, message.thrown
                    break;
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break;
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break;
            }
        }
    }
}
